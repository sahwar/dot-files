#!/usr/bin/env zsh

aliases_file=~/.ssh/host_aliases

if (( $# == 0 )) && [[ -r $aliases_file ]]; then
	# read aliases file and create an associative array
	typeset -A hosts
	while read line; do
		hosts[${${(z)line}[1]}]=${${(z)line}[2]}
	done < $aliases_file
	# initialize the host parameter with the first host found
	host=''
	if (( ${#${(k)hosts}} > 1 )); then
		host=${${(k)hosts}[1]}
	fi
	# load the completion system 
	autoload -Uz compinit
	compinit
	# add the available aliases as possible completions
	compdef "compadd ${(k)hosts}" -value-,host,-default-
	vared -hp "Enter hostname or alias>" host
	# resolve hostname from alias
	hostname=$hosts[$host]
	while [[ -z $host ]]; do
		echo "Error: Need to provide a hostname" >&2
		vared -hp "hostname/alias>" host
		hostname=$hosts[$host]
	done
	if [[ -z $hostname ]]; then
		hostname=$host
	fi
else
	hostname=$1
fi

if [[ -z $hostname ]]; then
	echo "Error: Empty hostname" >&2
	exit 1
fi

ssh -t $hostname "PATH=/usr/local/bin:/usr/bin:/bin zsh -l -c \"tmux attach || tmux new\""

if (( $? != 0 )); then
	echo "[Press Enter]"
	read
	exit 1
fi

exit
