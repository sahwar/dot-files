#compdef npm
#autoload

# npm completion, based this pip completion function:
# https://github.com/robbyrussell/oh-my-zsh/blob/master/plugins/pip/_pip

_npm_commands() {
	if (( ! $+npmcomp_commands )); then
		if [[ ! -r ~/.npmcomp-commands ]]; then
			echo -n " (saving npm command database to $HOME/.npmcomp-commands, please wait...)" 2> /dev/null
			local subcmd desc help
			for subcmd in `npm --help | grep '^\s' | grep -v '/'`; do
				subcmd=${subcmd:0:-1}
				desc=$(npm help $subcmd 2>/dev/null | grep -m1 '^\s\+npm-' |\
					awk -F'--' '{print $2}' 2> /dev/null)
				[[ -z $desc ]] && continue
				subcmd="$subcmd:$desc"
				echo $subcmd >> ~/.npmcomp-commands
			done
		fi
		npmcomp_commands=(${(f)"$(<~/.npmcomp-commands)"})
	fi
}

_npm_packages() {
	if (( ! $+npmcomp_packages )); then
		npmcomp_packages=()
		if [[ ! -r /tmp/npmcomp-packages ]]; then
			echo -n "(saving remote package database to /tmp/npmcomp-packages...)" 2> /dev/null
			npm search 2> /dev/null | sed '1d' | cut -d ' ' -f 1 >\
			 	/tmp/npmcomp-packages
		fi
		npmcomp_packages=(${(f)"$(</tmp/npmcomp-packages)"})
	fi
	_wanted npm_pkgs expl 'Npm packages' compadd -a npmcomp_packages
}

_npm_installed() {
	local installed_pkgs global p
	if [[ $words[(i)-g] -le $#words || $words[(i)--global] -le $#words ]]; then
		p=$(npm root -g)
	else
		p=$(npm root)
	fi

	if [[ -d $p ]]; then
		installed_pkgs=(`ls $p`)
	else
		installed_pkgs=()
	fi
	_wanted installed_pkgs expl 'Installed packages' compadd -a installed_pkgs
}

local expl
local -a all_pkgs installed_pkgs

_arguments \
	'(-v --version)'{-v,--version}'[show version number of program and exit]' \
	'(-h --help)'{-h,--help}'[show help]' \
	'*:: :->subcmds' && return 0

if (( CURRENT == 1 )); then
	_npm_commands
	_describe -t commands 'Subcommands' npmcomp_commands
	return
fi

case "$words[1]" in
	install|i)
		_arguments \
			'(-g --global)'{-g,--global}'[Install as a global package]' \
			'(--save)--save[Declare the package as a dependency in package.json]' \
			'(--save-dev)--save-dev[Declare the package as a development dependency in package.json]' \
			'(--save-optional)--save-optional[Declare the package as an optional dependency in package.json]' \
			'1: :->packages' &&  return 0

		[[ "$state" == packages ]] && _npm_packages
		;;
	show|view|v|info|repo)
		_arguments '1: :->packages' &&  return 0
		[[ "$state" == packages ]] && _npm_packages
		;;
	uninstall|unlink|un|remove|rm|r)
		_arguments \
			'(-g --global)'{-g,--global}'[Remove global package]' \
			'1: :->installed' &&  return 0
		[[ "$state" == "installed" ]] && _npm_installed
		;;
esac
