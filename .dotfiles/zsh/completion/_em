#compdef em

if [ -z "$EM_DIR" ]; then
	EM_DIR="$HOME/.env-manager"
fi

local curcontext=$curcontext state line\
	progs="$ZDOTDIR/functions/environment-manager/lib/*(/N:t)"

declare -A opt_args

_arguments\
	"1: :->prog"\
	'2: :->cmd'\
	'3: :->arg1'\
	'4: :->arg2'\
	'5: :->arg3'

case $state in
	prog)
		_arguments "1:Program:(${~progs})"
		;;
	cmd)
		_arguments "2:Command:(compile install uninstall add rm activate deactivate bin ls prev login)"
		;;
	arg1)
		case $words[3] in
			compile|install)
				_arguments '3:Version alias or number:((stable\:"The latest stable version" latest\:"The latest development version"))'
				;;
			uninstall|add)
				local versions="$EM_DIR/${words[2]}/versions/*(/N:t)"
				_arguments "3:Version name:(${~versions})"
				;;
			rm|activate|bin|which|use|as|-)
				local envs="$EM_DIR/${words[2]}/environments/*(/N:t)"
				_arguments "3:Select an environment:(${~envs})"
		esac
		;;
	arg2)
		case $words[3] in
			compile|install)
				_arguments '4:Alias for referencing the the environment: '
				;;
			add)
				_arguments '4:Environment name: '
				;;
		esac
		;;
	arg3)
		case $words[3] in
			compile)
				_arguments '5:Compilation flags(passed to --configure): '
				;;
		esac
		;;
esac
